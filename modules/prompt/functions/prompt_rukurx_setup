# original theme
function prompt_rukurx_precmd {
  branch_format="${_prompt_rukurx_colors[1]}(%b%f%u%c${_prompt_rukurx_colors[1]})%f"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  vcs_info 'prompt'
}

function prompt_rukurx_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_rukurx_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_rukurx_colors=(
      "%F{001}" # Red
      "%F{002}" # Green
      "%F{003}" # Yellow
      "%F{004}" # Blue
    )
  else
    _prompt_rukurx_colors=(
      "%F{red}"
      "%F{green}"
      "%F{yellow}"
      "%F{blue}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(${_prompt_rukurx_colors[1]}%b%f%u%c)"
  local action_format="(${_prompt_rukurx_colors[1]}|%a%f)"
  local unstaged_format="${_prompt_rukurx_colors[1]} *%f"
  local staged_format="${_prompt_rukurx_colors[1]} +%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}|${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Define prompts.
  PROMPT="${_prompt_rukurx_colors[2]}%n@%m%f:${_prompt_rukurx_colors[4]}%c%f "'${vcs_info_msg_0_}'"$ "
  RPROMPT=''
}

prompt_rukurx_setup "$@"
